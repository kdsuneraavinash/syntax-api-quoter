/*
 *  Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 *  WSO2 Inc. licenses this file to you under the Apache License,
 *  Version 2.0 (the "License"); you may not use this file except
 *  in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
plugins {
    id 'java'
}

description = 'Ballerina - Syntax API Quoter'

group 'io.ballerinalang'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    flatDir {
        dirs "${rootProject.projectDir}/libs"
    }
}

dependencies {
    implementation project(":quoter")
    implementation(group: 'commons-cli', name: 'commons-cli', version: '1.3.1') {
        because("To handle command line arguments")
    }
}

def mainCliClass = 'io.quoter.application.cli.QuoterCommandLine'

// Compile Configuration
compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

// Quoter command
task quoter(type: JavaExec) {
    main = mainCliClass
    classpath = sourceSets.main.runtimeClasspath
    if (project.hasProperty('props')) {
        //noinspection GroovyAssignabilityCheck
        args(project.property('props').split(' '))
    }
}

// Jar build command
task fatJar(type: Jar) {
    manifest { attributes 'Main-Class': mainCliClass }
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

ext.moduleName = 'io.quoter.application.cli'
